go-filepath src/turbine/wings
ts-filepath src/cockpit/wings

ts-import { IWingsStruct }:wings-ts-util
import src/wings/enum/city.wings
import src/wings/struct/day.wings
import src/wings/struct/user_access_level.wings

ts-implement IWingsStruct

# Trip - All information of a single trip.

struct Trip {
  id            int     -1
  user_id       int     -1
  name          str
  cities        []City
  days          []Day
  description   str
  time_created  date
  last_updated  date
  private       bool    true
  shared_with   []UserAccessLevel
}

go-func(
  // GetID (istruct) - Returns the trip ID.
  func (trip Trip) GetID() int {
    return trip.ID
  }

  // SetID (istruct) - Sets the trip ID.
  func (trip *Trip) SetID(id int) {
    trip.ID = id
  }
)
